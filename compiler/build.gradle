plugins {
    id('java')
    id('com.github.johnrengelman.shadow') version('6.1.0')
    id('maven-publish')
    id('signing')
}

group = 'io.github.jbock-java'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(16))
    }
}

compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = '11'
    targetCompatibility = '11'
    options.compilerArgs << '--release'
    options.compilerArgs << '11'
}

repositories {
    mavenCentral()
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.withType(GenerateModuleMetadata) {
    enabled = true
}

shadowJar {
    minimize()
    dependencies {
        exclude('io.github.jbock-java:jbock:.*')
        exclude('javax.annotation:jsr250-api:.*')
    }
    archiveClassifier.set('')
    relocate('dagger', 'net.jbock.dagger')
    relocate('com.squareup.javapoet', 'net.jbock.javapoet')
    relocate('com.google', 'net.jbock.google')
    relocate('org.checkerframework', 'net.jbock.org.checkerframework')
    relocate('javax.inject', 'net.jbock.javax.inject')
}

dependencies {
    def core = 'io.github.jbock-java:jbock:5.0'
    implementation('com.squareup:javapoet:1.13.0')
    implementation('com.google.auto:auto-common:1.0')
    implementation('com.google.guava:guava:30.1.1-jre')
    shadow(core)
    implementation('com.google.dagger:dagger:2.36')
    annotationProcessor('com.google.dagger:dagger-compiler:2.36')
    implementation('javax.annotation:jsr250-api:1.0')
    testImplementation(core)
    testImplementation('com.google.testing.compile:compile-testing:0.19')
    testImplementation('org.junit.jupiter:junit-jupiter:5.7.1')
    testImplementation('org.mockito:mockito-core:3.9.0')
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name': 'net.jbock.compiler',
                'Implementation-Version': project.properties['version']
        )
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events('failed')
    }
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}

artifacts {
    archives(javadocJar, sourcesJar)
}

// https://central.sonatype.org/pages/gradle.html
publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'jbock-compiler'

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'jbock-compiler'
                packaging = 'jar'
                description = 'jbock annotation processor'
                url = 'https://github.com/jbock-java/jbock'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'h908714124'
                        name = 'h908714124'
                        email = 'kraftdurchblumen@gmx.de'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jbock-java/jbock.git'
                    developerConnection = 'scm:git:https://github.com/jbock-java/jbock.git'
                    url = 'https://github.com/jbock-java/jbock'
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = System.getenv('OSS_USER')
                password = System.getenv('OSS_PASS')
            }
        }
    }
}

// https://docs.gradle.org/current/userguide/signing_plugin.html
signing {
    def signingKey = findProperty('signingKey')
    def signingPassword = findProperty('signingPassword')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
