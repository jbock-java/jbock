plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'com.github.h908714124'

compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = '8'
    targetCompatibility = '8'
}

// Prevent a problem with org.gradle.jvm.version
// https://docs.gradle.org/current/userguide/publishing_gradle_module_metadata.html
tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.squareup:javapoet:1.11.1'
    implementation 'com.github.h908714124:jbock-annotations:3.1'
    testImplementation 'com.google.testing.compile:compile-testing:0.18'
    testImplementation 'org.mockito:mockito-core:3.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.0-M1'
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name': 'net.jbock.compiler',
                'Implementation-Version': project.properties['version']
        )
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

// https://docs.gradle.org/current/userguide/signing_plugin.html
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        allprojects {
            ext."signatory.keyId" = System.getenv('SIGNING_KEY_ID')
            ext."signatory.password" = System.getenv('SIGNING_PASSWORD')
        }
    }
}

// https://central.sonatype.org/pages/gradle.html
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jbock'
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'jbock'
                packaging = 'jar'
                description = 'The jbock command line parser'
                url = 'https://github.com/h908714124/jbock'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'h908714124'
                        name = 'h908714124'
                        email = 'kraftdurchblumen@gmx.de'
                    }
                }
                scm {
                    connection = 'scm:svn:https://github.com/h908714124/jbock.git'
                    developerConnection = 'scm:svn:https://github.com/h908714124/jbock.git'
                    url = 'https://github.com/h908714124/jbock'
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = System.getenv('OSS_USER')
                password = System.getenv('OSS_PASS')
            }
        }
    }
}

signing {
    def signingKey = findProperty('signingKey')
    def signingPassword = findProperty('signingPassword')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
